plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = "${project.mod_version}-${project.minecraft_version}"
group = project.maven_group

ext.mod = { String localOverrideKey, Object dep ->
    File file = file("../.${localOverrideKey}-local");
    if (file.exists()) {
        dependencies.implementation(dep)
    } else {
        dependencies.modImplementation(dep)
    }
}

ext.includeMod = { String localOverrideKey, Object dep ->
    mod(localOverrideKey, dep)
    dependencies.include(dep)
}

repositories {
	maven {
		name = 'Fabric'
		url = 'https://maven.fabricmc.net/'
	}
	maven {
		name = 'TerraformersMC'
		url = 'https://maven.terraformersmc.com/'
	}	
	maven {
		name = 'JitPack'
		url = 'https://jitpack.io'
	}
	maven {
		name = 'ClothConfig'
		url = 'https://maven.shedaniel.me/'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	
	mod "fabric-loader", "net.fabricmc:fabric-loader:$project.loader_version"
	mod "modmenu", "com.terraformersmc:modmenu:$project.modmenu_version"
	modImplementation "net.fabricmc.fabric-api:fabric-api:$project.fabric_version"
	includeMod "cloth-config-2", "me.shedaniel.cloth:cloth-config-fabric:$project.clothconfig_version"
}

base {
	archivesName = project.archives_base_name
}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("MPL-SOFTWARE-UTILIZED")
	from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
